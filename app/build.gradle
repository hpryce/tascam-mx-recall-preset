/*
 * Tascam MX Preset Tool
 */

plugins {
    id 'application'
    alias(libs.plugins.graalvm.native)
}

repositories {
    mavenCentral()
}

dependencies {
    // Use JUnit Jupiter for testing.
    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Application dependencies
    implementation libs.guava
    implementation libs.picocli
    
    // Picocli annotation processor for GraalVM native-image
    annotationProcessor libs.picocli.codegen
}

// Java version compatibility
java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

application {
    mainClass = 'uk.co.hpnet.tascam.App'
}

tasks.named('test') {
    useJUnitPlatform()
}

graalvmNative {
    binaries {
        main {
            imageName = 'tascam-preset'
            mainClass = 'uk.co.hpnet.tascam.App'
            
            buildArgs.addAll(
                '--no-fallback',
                '-H:+ReportExceptionStackTraces'
            )
        }
    }
    
    toolchainDetection = false
}
